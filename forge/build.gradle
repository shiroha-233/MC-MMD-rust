plugins {
	id 'com.gradleup.shadow' version '8.3.6'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "mmdskin-forge.mixins.json"
    }
    // 启用 mixin refmap 生成
    mixin {
        defaultRefmapName = "mmdskin-forge-refmap.json"
    }
    runs {
        client {
            client()
            name "Forge Client"
            runDir "run/client"
            vmArgs "-Dneoforge.enabledGameTestNamespace=template-mod"
            programArgs "--username", "Player1"
        }
        server {
            server()
            name "Forge Server"
            runDir "run/server"
            vmArgs "-Dneoforge.enabledGameTestNamespace=template-mod"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it conflicts with Loom.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

shadowJar {
    exclude "architectury.common.json"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
    
    // 包含 common 模块的资源（原生库、默认动画等）
    from(project(":common").sourceSets.main.resources)
    
    // 排除不需要的文件
    exclude 'META-INF/versions/**'
    
    // 强制更新 META-INF/services 里的类名
    mergeServiceFiles()
    
    // 排除 module-info.class 避免 JPMS 冲突
    exclude 'module-info.class'
}


remapJar {
	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
}

jar {
	archiveClassifier.set("dev")
	from("${rootProject.projectDir}/LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
	
	manifest {
		attributes([
			'Specification-Title'     : 'Template Mod',
			'Specification-Vendor'    : 'Template Author',
			'Specification-Version'   : '1',
			'Implementation-Title'    : project.name,
			'Implementation-Version'  : project.version,
			'Implementation-Vendor'   : 'Template Author'
		])
	}
}

tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
        minecraft_version: '1.20.1',
        minecraft_version_range: '[1.20.1,1.21)',
        forge_version: project.forge_version,
        forge_version_range: '[47,48)',
        loader_version_range: '[47,)',
        mod_id: 'template-mod',
        mod_name: 'Template Mod',
        mod_version: project.version,
        mod_authors: 'Template Author',
        mod_description: 'Template Mod for Architectury'
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties
    }
}

dependencies {
    // Common 模块 - 添加源代码到编译路径
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }

    // JLayer - MP3 解码器（舞台模式音频播放）
    // common 配置使其在 dev 运行时可用（developmentForge extends common）
    // include 配置使其以 Jar-in-Jar 内嵌到 mod JAR
    common("javazoom:jlayer:1.0.1") { transitive = false }
    include("javazoom:jlayer:1.0.1")

    // Forge 平台
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Architectury API - 使用 Jar-in-Jar 内嵌
    modApi("dev.architectury:architectury-forge:9.2.14")
    include("dev.architectury:architectury-forge:9.2.14")
    
    // TouhouLittleMaid - 可选依赖，用于联动
    modCompileOnly("maven.modrinth:touhou-little-maid:1.5.0-forge+mc1.20.1")
    modLocalRuntime("maven.modrinth:touhou-little-maid:1.5.0-forge+mc1.20.1")
    
    // Cloth Config API - 配置界面
    modApi("me.shedaniel.cloth:cloth-config-forge:11.1.136")
}

// Source JAR 组装（合并 common 源码）
sourcesJar {
    // Avoid duplicate entries when merging common sources
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

sourceSets {
    main {
        java {
            // Avoid JPMS split-package: keep top-level network package only in common
        }
        resources {
            exclude 'assets/template-mod/lang/*.json'
        }
    }
}
