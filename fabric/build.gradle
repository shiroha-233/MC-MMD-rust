plugins {
	id 'com.gradleup.shadow' version '8.3.6'
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	mixin {
		defaultRefmapName = "mmdskin-fabric-refmap.json"
	}
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin since it conflicts with Loom.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

repositories {
	maven {
		url = "https://api.modrinth.com/maven"
	}
	maven {
		url "https://maven.shedaniel.me/"
		name "Shedaniel Maven"
	}
	// Sodium 和 Iris 的 Maven 仓库
	maven {
		url "https://maven.terraformersmc.com/releases/"
		name "TerraformersMC"
	}
	// Iris Maven
	maven {
		url "https://maven.irisshaders.dev"
		name "Iris Shaders"
	}
	// Cardinal Components API (Ladysnake)
	maven {
		url "https://maven.ladysnake.org/releases"
		name "Ladysnake"
	}
	// Reach Entity Attributes (JamiesWhiteShirt)
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release/"
		name "JamiesWhiteShirt"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
}

dependencies {
	implementation project(":common")
	common(project(path: ":common", configuration: "namedElements")) { transitive = false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }
	
	// JLayer - MP3 解码器（舞台模式音频播放，Fabric jar-in-jar 打包）
	implementation("javazoom:jlayer:1.0.1")
	include("javazoom:jlayer:1.0.1")
	
	// Fabric Loader
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// Architectury API - 内嵌到模组中
	modImplementation("dev.architectury:architectury-fabric:9.2.14") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include("dev.architectury:architectury-fabric:9.2.14")
	
	// Cloth Config API - 配置界面
	modApi("me.shedaniel.cloth:cloth-config-fabric:11.1.136") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	
	// Cardinal Components API - Touhou Little Maid Fabric 移植版的必需传递依赖
	// Modrinth Maven 不提供传递依赖，需要手动添加
	modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components_version}"
	modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${rootProject.cardinal_components_version}"
	modLocalRuntime "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components_version}"
	modLocalRuntime "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${rootProject.cardinal_components_version}"
	
	// Reach Entity Attributes - Touhou Little Maid 的传递依赖
	modLocalRuntime "com.jamieswhiteshirt:reach-entity-attributes:2.4.0"
	
	// Nashorn 脚本引擎 - Java 17 不再内置，Touhou Little Maid JS 动画需要
	runtimeOnly "org.openjdk.nashorn:nashorn-core:15.4"
	
	// TouhouLittleMaid Orihime (Fabric 移植版) - 可选依赖，用于联动
	modCompileOnly "maven.modrinth:touhoulittlemaid-orihime:${rootProject.touhou_little_maid_fabric_version}"
	modLocalRuntime "maven.modrinth:touhoulittlemaid-orihime:${rootProject.touhou_little_maid_fabric_version}"
	modLocalRuntime "maven.modrinth:forge-config-api-port:${rootProject.forge_config_api_port_version}"
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
    exclude "architectury.common.json"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
    
    // 排除不需要的文件
    exclude 'META-INF/versions/**'
    
    // 强制更新 META-INF/services 里的类名
    mergeServiceFiles()
    
    // 排除 module-info.class 避免 JPMS 冲突
    exclude 'module-info.class'
}

remapJar {
	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier.set(null)
}

jar {
	archiveClassifier.set("dev")
	from("${rootProject.projectDir}/LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}
sourcesJar {
    // Avoid duplicate entries when merging common sources
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def commonSources = project(':common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

sourceSets {
    main {
        java {
            // Avoid JPMS split-package with shaded common (network package) in dev runtime
        }
        resources {
            exclude 'assets/mmdskin/lang/*.json'
            exclude 'pack.mcmeta'
        }
    }
}
